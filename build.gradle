allprojects {
    group 'dev.xdark'
    version '2.0.0'
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'java-test-fixtures'
    apply plugin: 'maven-publish'

    repositories {
        mavenCentral()
        repositories {
            maven { url 'https://jitpack.io' }
        }
    }

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    tasks.withType(JavaCompile) {
        options.compilerArgs.addAll(['-parameters', '-g:lines,source,vars'])
    }

    task sourcesJar(type: Jar) {
        from sourceSets.main.java
        classifier = 'sources'
    }

    configurations {
        testFixturesAnnotationProcessor.extendsFrom testAnnotationProcessor
        testFixturesCompileOnly.extendsFrom testCompileOnly
    }

    dependencies {
        def junitVersion = '5.8.2'
        testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
        testImplementation "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
        testImplementation "org.junit.jupiter:junit-jupiter-params:$junitVersion"

        def lombok = 'org.projectlombok:lombok:1.18.24'
        compileOnly lombok
        annotationProcessor lombok

        testCompileOnly lombok
        testAnnotationProcessor lombok

        compileOnly 'org.jetbrains:annotations:23.0.0'
        testCompileOnly 'org.jetbrains:annotations:23.0.0'
    }

    test {
        useJUnitPlatform()
    }

    publishing {
        repositories {
            maven {
                name = 'sources-repo'
                url = 'file:///tmp/sources-repo'
            }
        }
        publications {
            maven(MavenPublication) {
                from components.java
                artifact sourcesJar
            }
        }
    }

    ext {
        asm = 'org.ow2.asm:asm:9.3'
        asm_tree = 'org.ow2.asm:asm-tree:9.3'
        cafedude = 'com.github.Col-E:CAFED00D:1.10.2'
        slf4j_api = 'org.slf4j:slf4j-api:1.7.36'
        jlinker = 'com.github.xxDark:jlinker:1.0.5'
    }
}